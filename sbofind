#!/usr/bin/env perl
#
# sbofind
# script to locate something in a local SlackBuilds tree.
#
# author: Jacob Pipkin <j@dawnrazor.net>
# date: Boomtime, the 39th day of Discord in the YOLD 3178
# license: WTFPL <http://sam.zoy.org/wtfpl/COPYING>

use SBO::Lib;
use File::Basename;
use Getopt::Std;
use strict;
use warnings FATAL => 'all';

my %config = %SBO::Lib::config;
my $self = basename ($0);

sub show_usage {
	print <<EOF
Usage: $self (search_term)

Options:
  -h: this screen.
  -v: version information.
  -i: show the .info for each found item.
  -r: show the README for each found item.

Example:
  $self libsexy 

EOF
}

my %options;
getopts ('hvir', \%options);

show_usage () and exit (0) if (exists $options{h});
show_version () and exit (0) if (exists $options{v});

my $show_readme = exists $options{r} ? 'TRUE' : 'FALSE';
my $show_info = exists $options{i} ? 'TRUE' : 'FALSE';

show_usage () and exit (1) unless exists $ARGV[0];
my $search = $ARGV[0];

# if we can't find SLACKBUILDS.TXT in $config{HOME}, prompt to fetch the tree
slackbuilds_or_fetch ();

# find anything with $search in its name
my (@findings, $name);
my $found = 'FALSE';
my $name_regex = qr/NAME:\s+(.*\Q$search\E.*)$/i;
my $loc_regex = qr/LOCATION:\s+(.*)$/;
my $fh = open_read ("$config{SBO_HOME}/SLACKBUILDS.TXT");
FIRST: while (my $line = <$fh>) {
	unless ($found eq 'TRUE') {
		if ($name = ($line =~ $name_regex)[0]) {
			$found = 'TRUE';
			next FIRST;
		}
	} else {
		if (my ($location) = ($line =~ $loc_regex)[0]) {
			$found = 'FALSE';
			$location =~ s#^\.##;
			push (@findings, {$name => $config{SBO_HOME} . $location} );
		}
	}
}

sub get_file_contents {
	script_error ('get_file_contents requires an argument') unless exists $_[0];
	script_error ('get_file_contents argument is not a file') unless -f $_[0];
	my $fh = open_read (shift);
	my $contents = do {local $/; <$fh>};
	$contents =~ s/\n/\n        /g;
	$contents =~ s/        $//g;
	return $contents;
}

# pretty formatting
if (exists $findings[0]) {
	my @listing = ("\n");
	for my $hash (@findings) {
		while (my ($key, $value) = each %{$hash}) {
			push (@listing, "SBo:    $key\n");
			push (@listing, "Path:   $value\n");
			push (@listing, "info:   ". get_file_contents
				("$value/$key.info") ) if $show_info eq 'TRUE';
			push (@listing, "README: ". get_file_contents ("$value/README") )
				if $show_readme eq 'TRUE';
			push (@listing, "\n");
		}
	}
	print $_ for @listing;
} else {
	print "Nothing found for search term: $search\n";
}

exit 0;
