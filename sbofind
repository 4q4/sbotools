#!/usr/bin/env perl
#
# vim: set ts=4:noet
#
# sbofind
# script to locate something in a local SlackBuilds tree.
#
# author: Jacob Pipkin <j@dawnrazor.net>
# date: Boomtime, the 39th day of Discord in the YOLD 3178
# license: WTFPL <http://sam.zoy.org/wtfpl/COPYING>

use 5.16.0;
use strict;
use warnings FATAL => 'all';
use SBO::Lib;
use File::Basename;
use Getopt::Std;

my $self = basename ($0);

sub show_usage () {
	print <<EOF
Usage: $self (search_term)

Options:
  -h: this screen.
  -v: version information.
  -i: show the .info for each found item.
  -r: show the README for each found item.

Example:
  $self libsexy 

EOF
}

my %options;
getopts ('hvir', \%options);

show_usage and exit 0 if exists $options{h};
show_version and exit 0 if exists $options{v};

my $show_readme = exists $options{r} ? 1 : 0;
my $show_info = exists $options{i} ? 1 : 0;

show_usage and exit 1 unless exists $ARGV[0];
my $search = $ARGV[0];

# if we can't find SLACKBUILDS.TXT in $config{HOME}, prompt to fetch the tree
slackbuilds_or_fetch; 

# find anything with $search in its name
sub perform_search ($) {
	exists $_[0] or script_error 'perform_search requires an argument.';
	my $search = shift;
	my (@findings, $name, $found);
	my $name_regex = qr/NAME:\s+(.*\Q$search\E.*)$/i;
	my $loc_regex = qr/LOCATION:\s+(.*)$/;
	my $fh = open_read "$config{SBO_HOME}/SLACKBUILDS.TXT";
	FIRST: while (my $line = <$fh>) {
		unless ($found) {
			$found++, next FIRST if $name = ($line =~ $name_regex)[0];
		} else {
			if (my ($location) = ($line =~ $loc_regex)[0]) {
				$found = 0;
				$location =~ s#^\.##;
				push @findings, {$name => $config{SBO_HOME} . $location};
			}
		}
	}
	return \@findings;
}

# pull the contents of a file into a variable and format it for output
sub get_file_contents ($) {
	exists $_[0] or script_error 'get_file_contents requires an argument';
	-f $_[0] or return "$_[0] doesn't exist.\n";
	my $fh = open_read shift;
	my $contents = do {local $/; <$fh>};
	$contents =~ s/\n/\n        /g;
	$contents =~ s/        $//g;
	return $contents;
}

perform_search $search;

# pretty formatting
if (exists $$findings[0]) {
	my @listing = ("\n");
	for my $hash (@$findings) {
		while (my ($key, $value) = each %$hash) {
			push @listing, "SBo:    $key\n";
			push @listing, "Path:   $value\n";
			push @listing, "info:   ". get_file_contents "$value/$key.info"
				if $show_info;
			push @listing, "README: ". get_file_contents "$value/README"
				if $show_readme; 
			push @listing, "\n";
		}
	}
	print $_ for @listing;
} else {
	say "Nothing found for search term: $search";
}

exit 0;
